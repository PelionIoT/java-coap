plugins {
    id 'java-library'
    id 'jacoco'
    id 'pmd'
    id "com.github.spotbugs"
    id 'maven-publish'
    id 'com.github.mfarsikov.kewt-versioning'
}

version = kewtVersioning.version

kewtVersioning.groovyConfigurationDsl {
    separator = ''
}

repositories {
    mavenCentral()
}

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    options.encoding = 'UTF-8'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

pmd {
    toolVersion = "5.6.1"
    consoleOutput = true
    sourceSets = [sourceSets.main]
    incrementalAnalysis = false
    ruleSets = []
    ruleSetFiles = files("../pmd-rules.xml")
}

spotbugs {
    effort = 'Max'
    excludeFilter = file("../spotbugs-exlude.xml")
}
spotbugsTest.enabled = false


// --- PUBLISHING ---

task sourceJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allJava
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/" + System.getenv("GITHUB_REPOSITORY"))
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            pom {
                name = 'Java CoAP'
                description = 'Java implementation of CoAP protocol'
                url = 'https://github.com/open-coap/java-coap'
                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }

            groupId 'com.github.open-coap.java-coap'
            artifact tasks.sourceJar
            from(components.java)
        }
    }
}
